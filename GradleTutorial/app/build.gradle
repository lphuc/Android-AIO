apply plugin: 'com.android.application'

android {

    compileSdkVersion 27
    buildToolsVersion '28.0.2'

    signingConfigs {
        release {
            storeFile file("keystore.jks")
            storePassword 'password'
            keyAlias 'alias'
            keyPassword 'keypass'
        }
    }

    /*provide the base configurations for all flavors here.
    * this block will contain options which will be applied to all build versions (e.g. debug, release...)*/
    defaultConfig {
        applicationId "com.example.phucengineer.gradletutorial"
        minSdkVersion 20
        targetSdkVersion 27
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        signingConfig signingConfigs.release
    }

    /*Android studio automatically creates the debug & release types*/
    buildTypes {
        release {
            signingConfig signingConfigs.release
            shrinkResources true
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            minifyEnabled true
            shrinkResources false
            useProguard false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }


    /*similar to buildTypes, support same properties as defaultConfig, all flavors must belong to a named flavor dimension
* even if we intent to use only a single dimension.*/
    //specify a flavor dimension named "version"
    flavorDimensions "mode", "api"
    //add "production", "localTest" product flavors, these flavors provide their own applicationIdSuffix and versionNameSuffix
    productFlavors {
        production {
            dimension "mode"
            applicationIdSuffix ".production"
            versionNameSuffix "-production"
            buildConfigField 'String', 'HOST', '"http://public-host.com/"'
            buildConfigField 'String', 'VERSION', '"api-name/"'
            buildConfigField 'Boolean', 'DEBUG_MODE', 'false'
        }
        local {
            // Assigns this product flavor to the "version" flavor dimension.
            // This property is optional if you are using only one dimension.
            dimension "mode"
            applicationIdSuffix ".local"
            versionNameSuffix "-local"
            buildConfigField 'String', 'HOST', '"http://localhost/"'
            buildConfigField 'String', 'VERSION', '"api-name/"'
            buildConfigField 'Boolean', 'DEBUG_MODE', 'true'
        }
        minApi19 {
            dimension "api"
            minSdkVersion(19)
            versionNameSuffix = "-minApi19"
        }
        minApi23 {
            dimension "api"
            minSdkVersion(23)
            versionNameSuffix = "-minApi23"
        }
    }


    splits {
        /*setting to generate multiple APK files based on screen density*/
        density {
            // enable/disable building multi APKs
            enable false

            // exclude these densities below when buildingg
            exclude "ldpi", "tvdpi"

            // Specifies a list of compatible screen size settings for the manifest.
            compatibleScreens 'small', 'normal', 'large', 'xlarge', 'xxlarge', 'xxxlarge'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'com.android.support:appcompat-v7:27.1.1'
    implementation 'com.android.support.constraint:constraint-layout:1.1.3'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
    implementation 'com.squareup.dagger:dagger:1.2.1'
}
